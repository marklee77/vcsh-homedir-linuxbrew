export LINUXBREW_HOME="${HOME}/.linuxbrew"
export PATH="${LINUXBREW_HOME}/bin:${LINUXBREW_HOME}/sbin:$PATH"
export MANPATH="${LINUXBREW_HOME}/share/man:$MANPATH"
export INFOPATH="${LINUXBREW_HOME}/share/info:$INFOPATH"

# this function adds the linuxbrew xdg data dirs and ld library paths for
# commands run from the linuxbrew home, and removes them for other commands.
function linuxbrew_setup_env {
    args=("${(@s/ /)1}")
    cmd=$(whence -p ${args[1]})
    if [[ "$cmd" == ${LINUXBREW_HOME}/* ]]; then
      export XDG_DATA_DIRS="${LINUXBREW_HOME}/share:$XDG_DATA_DIRS"
      export LD_LIBRARY_PATH="${LINUXBREW_HOME}/lib:$LD_LIBRARY_PATH"
    else
      xdg_data_dirs=("${(@s.:.)XDG_DATA_DIRS}")
      ld_library_path=("${(@s.:.)LD_LIBRARY_PATH}")
      export XDG_DATA_DIRS="${(@j.:.)xdg_data_dirs:#${LINUXBREW_HOME}/share}"
      export LD_LIBRARY_PATH="${(@j.:.)ld_library_path:#${LINUXBREW_HOME}/lib}"
    fi
}

typeset -U preexec_functions
preexec_functions=(linuxbrew_setup_env $preexec_functions)
